WHITESPACE = _{ " " }
plural_fino = { "j" | "J" }
akuzativa_fino = { "n" | "N" }
cxapela_litero = _{ "ĉ" | "ĝ" | "ĥ" | "ĵ" | "ŝ" | "ŭ" | "Ĉ" | "Ĝ" | "Ĥ" | "Ĵ" | "Ŝ" | "Ŭ" }
litero = _{ASCII_ALPHA | cxapela_litero}

////
//  Tablevortoj.
////
prefikso = { "ki" | "ti" | "i" | "ĉi" | "neni" }
postfikso = { "om" | "es" | "el" | "e" | "am" | "al" }
sxangxebla_postfikso = { "o" | "u" | "a" | "e"}
tabelvorto = ${ prefikso ~ (postfikso | (sxangxebla_postfikso ~ plural_fino* ~ akuzativa_fino* ))}

////
//  Vortetoj.
////
e_vorteto = {
    "ĉi" | "for" | "ankoraŭ" | "baldaŭ" | "hodiaŭ" | "hieraŭ" | "morgaŭ" | "jam" |
    "ĵus" | "nun" | "tuj" | "ajn" | "almenaŭ" | "ankaŭ" | "apenaŭ" | "do" | "eĉ"  |
    "ja" | "jen" | "kvazaŭ" | "mem" | "nur" | "plu" | "plej" | "preskaŭ" |
    "tamen" | "tro" | "tre"
}
rolvorteto = {
    "je" | "de" | "da" | "antaŭ" | "apud" | "ĉe" | "ĉirkaŭ" | "ekster" | "en" |
    "inter" | "kontraŭ" | "post" | "preter" | "sub" | "super" | "sur" | "tra" |
    "trans" | "al" | "el" | "ĝis" | "anstataŭ" | "dum" | "krom" | "kun" | "laŭ" |
    "malgraŭ" | "per" | "po" | "por" | "pri" | "pro" | "sen"
}
gramatika_vorteto = {
    "kaj" | "aŭ" | "sed" | "plus" | "minus" | "ĉu" | "jen" | "ju" | "des" |
    "nek" | "ol" | "ke" | "ne" | "jes"
}
ekkriita_vorto = { "adiaŭ" | "bis" | "fi" | "ha" | "ho" | "hura" | "nu" | "ve" }


////
//  Nombroj.
////
baz_nombro = _{
    "unu" | "du" | "tri" | "kvar" | "kvin" | "ses" | "sep" | "ok" | "naŭ" |
    "dek" | "cent" | "mil" | "miliono" | "miliardo"
}
nombro = { baz_nombro+ }

////
// Pronomoj.
////
baz_pronomo = _{ "mi" | "ci" | "vi" | "ili" | "ŝi" | "li" | "ri" | "ni" | "si" | "ĝi" }
pronomo = @{baz_pronomo ~ ("a")* ~ (plural_fino)* ~ (akuzativa_fino)*}


////
// Substantivoj.
////
ne_apostrof = { (!"'" ~ litero)* ~ ("'") }
ne_o = { (!"o" ~ litero)* ~ ("o") }
ne_oj = { (!"oj" ~ litero)* ~ ("oj") }
ne_on = { (!"on" ~ litero)* ~ ("on") }
ne_ojn = { (!"ojn" ~ litero)* ~ ("ojn") }
substantivo = ${
    (ne_ojn+ ~ !litero{3}) |
    (ne_on+ ~ !litero{2}) |
    (ne_oj+ ~ !litero{2}) |
    (ne_o+ ~ !litero) |
    (ne_apostrof+ ~ !litero)
}

////
// Adjektivoj.
////
ne_a = { (!"a" ~ litero)* ~ ("a") }
ne_aj = { (!"aj" ~ litero)* ~ ("aj") }
ne_an = { (!"an" ~ litero)* ~ ("an") }
ne_ajn = { (!"ajn" ~ litero)* ~ ("ajn") }
adjektivo = ${
    (ne_ajn+ ~ !litero{3}) |
    (ne_an+ ~ !litero{2}) |
    (ne_aj+ ~ !litero{2}) |
    (ne_a+ ~ !litero)
}

////
// Adverboj.
////
ne_e = { (!"e" ~ litero)* ~ "e" }
ne_en = { (!"en" ~ litero)* ~ "en" }
adverbo = ${
    (ne_en+ ~ !litero{2}) |
    (ne_e+ ~ !litero)
}

////
// Verboj.
////
infinitivo = { (!"i" ~ litero)* ~ ("i") }
pastinecto = { (!"is" ~ litero)* ~ ("is") }
nuntempo = { (!"as" ~ litero)* ~ ("as") }
estonteco = { (!"os" ~ litero)* ~ ("os") }
kondixa = { (!"us" ~ litero)* ~ ("us") }
impterativo = { (!"u" ~ litero)* ~ ("u") }
verbo = @{
    (kondixa+ ~ !litero{2}) |
    (impterativo+ ~ !litero) |
    (pastinecto+ ~ !litero{2}) |
    (nuntempo+ ~ !litero{2}) |
    (estonteco+ ~ !litero{2}) |
    (infinitivo+ ~ !litero)
}

////
// Vortoj.
////
vorto = {
    tabelvorto | e_vorteto | rolvorteto | gramatika_vorteto | ekkriita_vorto |
    pronomo | nombro | adjektivo | substantivo | adverbo | verbo
}

//literoj = @{ (ESPERANTA_LITERO)+ }
frazo = { (vorto ~ (" ")*)+ }
dosiero = { SOI ~ (vorto ~ (" ")+)* ~ EOI }
